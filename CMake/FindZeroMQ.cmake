IF(NOT ZEROMQ_ROOT_DIR AND NOT $ENV{ZEROMQ_ROOT_DIR} STREQUAL "")
  SET(ZEROMQ_ROOT_DIR $ENV{ZEROMQ_ROOT_DIR})
ENDIF()

SET(_ZEROMQ_SEARCH_DIRS
  ${ZEROMQ_ROOT_DIR}
  /usr/local
  /sw # Fink
  /opt/local # DarwinPorts
  /opt/csw # Blastwave
  /opt/lib/oiio
)

FIND_PATH(ZEROMQ_INCLUDE_DIR
  NAMES         zmq.h
  HINTS         ${_ZEROMQ_SEARCH_DIRS}
  PATH_SUFFIXES include
)

FIND_LIBRARY(ZEROMQ_LIBRARY
  NAMES         zmq
  HINTS         ${_ZEROMQ_SEARCH_DIRS}
  PATH_SUFFIXES lib64 lib
)

# handle the QUIETLY and REQUIRED arguments and set ZEROMQ_FOUND to TRUE if 
# all listed variables are TRUE
INCLUDE(FindPackageHandleStandardArgs)
FIND_PACKAGE_HANDLE_STANDARD_ARGS(ZEROMQ DEFAULT_MSG
    ZEROMQ_LIBRARY ZEROMQ_INCLUDE_DIR)

IF(ZEROMQ_FOUND)
  SET(ZEROMQ_LIBRARIES ${ZEROMQ_LIBRARY})
  SET(ZEROMQ_INCLUDE_DIRS ${ZEROMQ_INCLUDE_DIR})
ENDIF(ZEROMQ_FOUND)

MARK_AS_ADVANCED(
  ZEROMQ_INCLUDE_DIR
  ZEROMQ_LIBRARY
)
